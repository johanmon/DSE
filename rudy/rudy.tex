\documentclass[a4paper, 11pt]{article}

\usepackage{ifthen}
\usepackage[latin1]{inputenc}

\newcommand{\nnsection}[1]{
\section*{#1}
\addcontentsline{toc}{section}{#1}
}

\begin{document}

\begin{center}
\vspace{20pt}
\textbf{\large Rudy: a small web server}\\
\vspace{10pt}
\textbf{Johan Montelius}\\ 
\vspace{10pt}
\textbf{\today}
\end{center}

\nnsection{Introduction}

Your task is to implement a small web server in Erlang. The aim of
this exercise is that you should be able to:

\begin{itemize}
\item describe the procedures for using a socket API
\item describe the structure of a server process
\item describe the HTTP protocol 
\end{itemize}

As a side effect you will also learn how to use do some Erlang programming.

\section{A HTTP parser}

Let's start by implementing a HTTP parser. We will not build a
complete parser nor will we implement a server that can reply to 
queries (yet) but we will do enough to understand how Erlang works and
how HTTP is defined.

\subsection{a HTTP request}

Open a file {\tt http.erl} and declare a module {\tt http} on the
first line. Also export the functions that we will use from the
outside of the module. In the end we will not export everything but
while you're debugging you want to test the functions as we implement them.


\begin{verbatim}
-module(http).
-export([parse_request/1]).
\end{verbatim}

We're only going to implement the parsing of a HTTP get request; and
avoiding some details to make life easier. Download the RFC 2616 from
www.ietf.org and follow the descriptions of a request. From the RFC we have:


\begin{verbatim}
  Request        = Request-Line               ; Section 5.1
                   *(( general-header         ; Section 4.5
                     | request-header         ; Section 5.3
                     | entity-header ) CRLF)  ; Section 7.1
                   CRLF
                   [ message-body ]           ; Section 4.3
\end{verbatim}


So a request consist of: a request line, an optional sequence of
headers, a carriage return line feed (CRLF) and, an optional body. We also
not that each header is terminated by a carriage return line feed. OK,
let's go; we implement each parsing function so that it will parse its
element and return a tuple consisting of the parsed result and the
rest of the string.

\begin{verbatim}
parse_request(R0) ->
    {Request, R1} = request_line(R0),
    {Headers, R2} = headers(R1),
    {Body, _} = message_body(R2),
    {Request, Headers, Body}.
\end{verbatim}

\subsection{the request line}

Now take a look at the RFC and the definition of the request line.

\begin{verbatim}
      Request-Line   = Method SP Request-URI SP HTTP-Version CRLF
\end{verbatim}

The request line consist of: a method, a request URI and, a http
version. All separated by space characters and terminated by a
carriage return and line feed.


The method is one of: {\tt OPTIONS}, {\tt GET}, {\tt HEAD} etc. Since we
are only interested in get requests, life becomes easy. 

To understand how we implement the parser you must know how strings
are represented in Erlang. Strings are lists of integers and one way
of writing an integer is {\tt \$G}, that is the ASCII value of the
character G. So the string ``GET'' could be written {\tt [\$G,\$E,\$T]}, or (if you know your ASCII) as
{\tt [71,69,84]}. Now let's do some string parsing.

\begin{verbatim}
request_line([$G, $E, $T, 32 |R0]) ->
    {URI, R1} = request_uri(R0),
    {Ver, R2} = http_version(R1),
    [13,10|R3] = R2,
    {{get, URI, Ver}, R3}.
\end{verbatim}

We match the input string with a list starting with the integers of G, E and T,
followed by 32 (which is the ASCII value for space). After having
matched the input string with ``GET '' we continue with the rest of
the string {\tt R0}. We find the URI, the version and finally, the
carriage return and line feed that is the end of the request line. 

We then return the tuple {\tt \{\{get, URI, Ver\}, R3\}}, the first
element is the parsed representation of the request line and {\tt R3}
is the rest of the string.

If you have problem following the description and have not yet started
to read the Erlang ``Getting Started'' section, it is now high time to
do so. 

\subsection{the URI}

Next we implement the parsing of the URI. This requires recursive
definitions. If you have not been exposed to functional
programing this might twist your head. If you have done some functional
programming you might say ``this is not tail recursive, I can do it
better'', that is fine I only wanted to make things easy and not
introduce things that we don't need. 

\begin{verbatim}
request_uri([32|R0])->
    {[], R0};
request_uri([C|R0]) ->
    {Rest, R1} = request_uri(R0),
    {[C|Rest], R1}.
\end{verbatim}

The URI is returned as a string. There is of course a whole lot of
structure to that string. We will find the resource that we are
looking for possibly with query information etc. For now we leave it
as a string, but feel free to parse it later.

\subsection{version information}

Parsing the version is simple, it's either version ``1.0'' or
``1.1''. We represent this by the atoms {\tt v11} and {\tt v10}. This
means that we later can switch on the atom rather than again parsing a
string. It does of course mean that our program will stop working when
1.2 is release but that will probably not be this week.

\begin{verbatim}
http_version([$H, $T, $T, $P, $/, $1, $., $1 | R0]) ->
    {v11, R0};
http_version([$H, $T, $T, $P, $/, $1, $., $0 | R0]) ->
    {v10, R0}.
\end{verbatim}

\subsection{headers}

Headers also have internal structure but we are only interested in
dividing them up into individual strings and most important find the
end of the header section. We implement this as two recursive functions;
one that consumes a sequence of headers and one that consumes
individual headers.


\begin{verbatim}
headers([13,10|R0]) ->
    {[],R0};
headers(R0) ->
    {Header, R1} = header(R0),
    {Rest, R2} = headers(R1),
    {[Header|Rest], R2}.

header([13,10|R0]) ->
    {[], R0};
header([C|R0]) ->
    {Rest, R1} = header(R0),
    {[C|Rest], R1}.     
\end{verbatim}    

\subsection{the body}

The last thing we need is parsing of the body and we will make things
very easy (even cheating). We assume that the body is everything that
is left but the truth is not that simple. If we call our function with
a string as input argument there is little discussion of how large the
body is but this is not easy if we want to parse an incoming stream
of bytes. When do we reach the end, when should we stop waiting for
more? The length of the body is therefore encoded in the headers of
the request. Or rather in the specification of HTTP 1.0 and 1.1 there
are several alternative ways of determining the length of the
body. If you dig deeper into the specs you will find that it is quite
messy. In our little world we will however treat the rest of the string
as the body.

\begin{verbatim}
message_body(R) ->
    {R, []}.
\end{verbatim}    

\subsection{a small test}

You now have all the pieces and if you compile and load the module in
a Erlang shell you can parse a request. Call the function with the
http module prefix and give it a string to parse.

\begin{verbatim}
7>c(http).
{ok,http}
8>http:parse_request("GET /index.html HTTP/1.1\r\nfoo 34\r\n\r\nHello").
{{get,"/index.html",v11},["foo 34"],"Hello"}
9> 
\end{verbatim}

\subsection{what about replies}

We will not deliver very interesting replies from our server but here
is a function that generates a HTTP reply with a 200 status code (200
is all OK). Another function can be convenient to have when we want to
generate a request. Also export these from the http module, we are
going to use them later.

\begin{verbatim}
ok(Body) ->
    "HTTP/1.1 200 OK\r\n" ++ "\r\n" ++ Body.

get(URI) ->
    "GET " ++ URI ++ " HTTP/1.1\r\n" ++ "\r\n".
\end{verbatim}

Note the double {\tt $\backslash r\backslash n$}, one to end the
status line and one to end the header section. A proper reply should
containing headers that describe the content and the size of the body
but a normal browser will understand what we mean.

If you have had problems so far I suggest that you study the ``Getting
Started'' section and go through the tutorials found on the web before
carrying on. If you have not had any problems so far, please continue.

\section{The first reply}

Your task now is to start a program that waits for an incoming
request, delivers a reply and then terminates. This is not much of a
web server but it will show you how to work with sockets. The
important lesson here is that a socket that a server listen to is not
the same thing as the socket later used for communication. 

Call the first test rudy, open a new file and add a module
declaration. You should define four procedures:

\begin{itemize}
\item {\tt init(Port)}: the procedure that will initialize the server, takes a
port number (for example 8080), opens a listening socket and passes the
socket to {\tt handler/1}. Once the request has been handled the socket
will be closed.

\item {\tt handler(Listen)}: will listen to the socket for an incoming
connection. Once a client has connect it will pass the connection to
{\tt request/1}. When the request is handled the connection is closed.

\item {\tt request(Client)}: will read the request from the client
connection and parse it. It will then parse the request using your
http parser and pass the request to {\tt reply/1}. The reply is then
sent back to the client.

\item {\tt reply(Request)}: this is where we decide what to
reply, how to turn the reply into a well formed HTTP reply.
\end{itemize}

The program could have the following structure (the ``:'' are open for you to fill in):

\begin{verbatim}
init(Port) ->
    Opt = [list, {active, false}, {reuseaddr, true}],
    case gen_tcp:listen(Port, Opt) of
        {ok, Listen} -> 
            :
            gen_tcp:close(Listen),
            ok;
        {error, Error} -> 
            error
    end.

handler(Listen) ->
    case gen_tcp:accept(Listen) of
        {ok, Client} ->
            :
        {error, Error} ->
            error
     end.

request(Client) ->
    Recv = gen_tcp:recv(Client, 0),
    case Recv of
        {ok, Str} ->
            :
            Response = reply(Request),
            gen_tcp:send(Client, Response);
        {error, Error} ->
            io:format("rudy: error: ~w~n", [Error])
    end,
    gen_tcp:close(Client).
    
reply({{get, URI, _}, _, _}) ->    
    http:ok(...).
\end{verbatim}


\subsection{Socket API}

In order to implement the above procedures you will need the functions
defined in the {\tt gen\_tcp} library. Look up the library in the
Kernel Reference Manual found under ``Basic/kernel'' in the
documentation. The following will get you starting:

\begin{itemize} 

\item {\tt gen\_tcp:listen(Port, Option)}: this is how a listening
socket is opened by the server. We will pass the port number as an
argument and use the following option: {\tt list}, {\tt \{active,
false\}}, {\tt \{reuseaddr, true\}}. Using these option we will see the
bytes as a list of integers instead of a binary structure. We will
need to read the input using {\tt recv/2} rather than having it sent
to us as messages. The port address should be used again and again. 

\item {\tt gen\_tcp:accept(Listen)}: this is how we accept an incoming
request. If it succeeds we will have a communication channel open to
the client.

\item {\tt gen\_tcp:recv(Client, 0)}: once we have the connection to the
Client we will read the input and return it as a string. The {\tt 0}
augment 0, tells the system to read as much as possible.

\item {\tt gen\_tcp:send(Client, Reply)}: this is how we send back a reply,
in the form of a string, to the client.

\item {\tt gen\_tcp:close(Socket)}: once we are done we need to close the
connection. Note that we also have to close the listening socket that
we opened in the beginning.
\end{itemize}

Fill in the missing pieces, compile and start the program. Use your
browser to retrieve the ``page'' by accessing
``http://localhost:8080/foo''. Any luck?

\subsection{a server}

Now a server should of course not terminate after one request. The
server should run and provide a service until it is manually
terminated. To achieve this we need to listen to a new connection once
the first has been handled. This is easily achieved by modifying {\tt
  handler/1} procedure so that it calls itself recursively once the
first request has been handled.

A problem is of course how to terminate the server. If it is suspended
waiting for an incoming connection the only way to terminate it is to
kill the process. You don't want to kill the Erlang shell so one
solution is to let the server run as a separate Erlang process and then
register this process under a name in order to kill it.

\begin{verbatim}
-export([start/1, stop/0]).

start(Port) ->
    register(rudy, spawn(fun() -> init(Port) end)).

stop() ->
    exit(whereis(rudy), "time to die").
\end{verbatim}    

This is quite brutal and one should of course do things in a more
controlled manner but it works for now.

\section{The assignment}

You should complete the rudimentary server described above and do some
experiments. Set up the server on one machine and access it from
another machine. A small benchmark program can generate requests
and measure the time it takes to receive the answers.

\begin{verbatim}
-module(test).
-export([bench/2]).


bench(Host, Port) ->
    Start = erlang:system_time(micro_seconds),
    run(100, Host, Port),
    Finish = erlang:system_time(micro_seconds),
    Finish - Start.


run(N, Host, Port) ->
    if 
        N == 0 ->
            ok;
        true ->
            request(Host, Port),
            run(N-1, Host, Port)
    end.


request(Host, Port) ->
    Opt = [list, {active, false}, {reuseaddr, true}],
    {ok, Server} = gen_tcp:connect(Host, Port, Opt),
    gen_tcp:send(Server, http:get("foo")),
    Recv = gen_tcp:recv(Server, 0),
    case Recv of
        {ok, _} ->
            ok;
        {error, Error} ->
            io:format("test: error: ~w~n", [Error])
    end,
    gen_tcp:close(Server).
\end{verbatim}

Remove the print-out calls so that we're measuring the performance of
the server and not the performance of the output function. Insert a
small delay (40ms) in the handling of the request to simulate file
handling, server side scripting etc.

\begin{verbatim}
reply({{get, URI, _}, _, _}) ->    
    timer:sleep(40),
    http:ok(...).
\end{verbatim}

Now, how many request per second can we serve? Is our artificial delay
significant or does it disappear in the parsing overhead? What happens
if you run the benchmarks on several machines at the same time? Run
some tests and report your findings.

\section{Going further}

At the seminar we will discuss and implement some improvements to the
server. The most important is a multi-threaded extension.

\subsection{Increasing throughput}

Our web server as it looks right now will wait for a request, server
the request and wait for the next. If the serving of a request depends
on other processes, such as the file system or some database, we will
be idle waiting while a new request might all ready be available for
parsing. We want to increase the throughput of our server by letting
each request be handled concurrently.

Should we create a new process for each incoming reply? Does it not
take time to create a process? What will happen if we have thousand of
requests a minute? A better approach might be to create a pool of
handlers and assign request to them; if there are now available
handlers the request will be put on hold or ignored. 

Erlang even allows several processes to listen to a socket. One could
create a fixed number of sequential servers that are all listening to
the same socket.

The Erlang system has support for multiprocessor architectures. If you
have dual-core handy you can do some performance testing. Read more on
how to start Erlang with multiprocessor support. Things might even
improve a single core processor since the system will be better in
hiding latencies.


\subsection{HTTP parsing}

If you have done things the easy way you might have taken for granted
that the whole HTTP request is complete in the first string that you
read from the socket. This might not be that case, the request might be
divided into several blocks and you will have to concatenated these
before you can read a complete request. 

One simple solution is as follows: do a first scan of the string and
look for a double CR, LF. This will be the end of the header section;
if you don't find it you will have to wait for more. How do you know
how long the body is?

\subsection{Deliver files}

It's not much of an web server if it can only deliver canned
answers. Let's extend the server so that it can deliver files. In
order to do this we need to parse the URI request and separate the
path and file form a possible query or index. Once we have the file in
our hand we need to make up a proper reply header containing the
proper size, type and coding descriptors.

\subsection{robustness}

The way the server is terminated is of course not very elegant. One
could do a much better job by having the sockets being active and
deliver the connections as messages. The server could then be free to
receive either control messages from a controlling process or a message
from the socket. 

Another problem with the current implementation is that it will leave
sockets open if things go wrong. A more robust way is to trap
exceptions and close sockets, open files etc before terminating. 



\end{document}
